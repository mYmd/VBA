Option Explicit

'関数適用関数 applyFun  引数を左に書く
'showM mapR(p_applyFun, 3, Array(p_exp, p_log))
'showM zipWith(p_applyFun, Array(3, 5), Array(p_exp, p_log))
'この関数をfoldl scanlで繋げることで関数合成ができる（foldrやscanrに渡してはいけない）
'showM unzip(scanl(p_applyFun, Array(0, 1), repeat(p_fibonacci, 10)), 2)
Function applyFun(ByRef param As Variant, ByRef func As Variant) As Variant
    applyFun = simple_invoke(func, param)
End Function
Function p_applyFun() As Long
    p_applyFun = VBA.CLng(AddressOf applyFun)
End Function

'============================================================================

'指数関数
Function expN(ByRef elem As Variant, ByRef dummy As Variant) As Variant
    expN = Exp(elem)
End Function
Function p_exp() As Long
    p_exp = VBA.CLng(AddressOf expN)
End Function

'対数関数
Function logN(ByRef elem As Variant, Optional ByRef check As Variant = vbFalse) As Variant
    If IsMissing(check) Then
        logN = Log(elem)
    ElseIf check <> vbFalse Then
        If IsNumeric(elem) Then
            If 0 < elem Then
                logN = Log(elem)
            Else
                logN = 0
            End If
        Else
            logN = 0
        End If
    Else
        logN = Log(elem)
    End If
End Function
Function p_log() As Long
    p_log = VBA.CLng(AddressOf logN)
End Function

'加算
Function add(ByRef elem As Variant, ByRef a As Variant) As Variant
    If IsNumeric(elem) And IsNumeric(a) Then
        add = elem + a
    End If
End Function
Function p_add() As Long
    p_add = VBA.CLng(AddressOf add)
End Function

'減算
Function minus(ByRef elem As Variant, ByRef a As Variant) As Variant
    If IsNumeric(elem) And IsNumeric(a) Then
        minus = elem - a
    End If
End Function
Function p_minus() As Long
    p_minus = VBA.CLng(AddressOf minus)
End Function

'乗算
Function mult(ByRef elem As Variant, ByRef a As Variant) As Variant
    If IsNumeric(elem) And IsNumeric(a) Then
        mult = elem * a
    End If
End Function
Function p_mult() As Long
    p_mult = VBA.CLng(AddressOf mult)
End Function

'除算
Function divide(ByRef elem As Variant, ByRef a As Variant) As Variant
    If IsNumeric(elem) And IsNumeric(a) Then
        divide = elem / a
    End If
End Function
Function p_divide() As Long
    p_divide = VBA.CLng(AddressOf divide)
End Function

'多項式　（係数は高次->低次）
Function poly(ByRef elem As Variant, ByRef coef As Variant) As Variant
    Dim z As Variant
    poly = 0
    For Each z In coef:    poly = poly * elem + z:      Next z
End Function
Function p_poly() As Long
    p_poly = VBA.CLng(AddressOf poly)
End Function

'2点間の距離
Function distance(ByRef x As Variant, Optional ByRef y As Variant) As Variant
    Dim i As Long
    
    distance = 0#
    If IsMissing(y) Then
        For i = 0 To sizeof(x) - 1 Step 1
            distance = distance + x(i + LBound(x)) ^ 2
        Next i
    Else
        For i = 0 To sizeof(x) - 1 Step 1
            distance = distance + (x(i + LBound(x)) - y(i + LBound(y))) ^ 2
        Next i
    End If
    distance = distance ^ 0.5
End Function
Function p_distance() As Long
    p_distance = VBA.CLng(AddressOf distance)
End Function

'乱数
Function vbRand(ByRef from_ As Variant, ByRef to_ As Variant) As Variant
    vbRand = (to_ - from_) * Rnd() + from_
End Function
Function p_rnd() As Long
    p_rnd = VBA.CLng(AddressOf vbRand)
End Function

'min
Function min(ByRef a As Variant, ByRef b As Variant) As Variant
    min = IIf(a < b, a, b)
End Function
Function p_min() As Long
    p_min = VBA.CLng(AddressOf min)
End Function

'max
Function max(ByRef a As Variant, ByRef b As Variant) As Variant
    max = IIf(a < b, b, a)
End Function
Function p_max() As Long
    p_max = VBA.CLng(AddressOf max)
End Function

'フィボナッチ関数   (1, 1)->(1, 2)->(2, 3)->(3, 5)->(5, 8)-> ...
' showM foldl(p_compoL, repeat(p_fibonacci, N), Array(0, 1))
Function fibonacci(ByRef a As Variant, ByRef dummy As Variant) As Variant
    fibonacci = Array(a(1 + LBound(a)), a(LBound(a)) + a(1 + LBound(a)))
End Function
Function p_fibonacci() As Long
    p_fibonacci = VBA.CLng(AddressOf fibonacci)
End Function

'ロジスティック写像   (Xn, r)->(Xn+1, r)
Function Logistic(ByRef x As Variant, ByRef dummy As Variant) As Variant
    'Xn+1 = a * Xn * (1-Xn)
    Logistic = Array(x(1) * x(0) * (1 - x(0)), x(1))
End Function
Function p_Logistic() As Long
    p_Logistic = VBA.CLng(AddressOf Logistic)
End Function

'述語 less
Function less(ByRef a As Variant, ByRef b As Variant) As Variant
    less = IIf(a < b, 1&, 0&)
End Function
Function p_less() As Long
    p_less = VBA.CLng(AddressOf less)
End Function

'述語 great
Function great(ByRef a As Variant, ByRef b As Variant) As Variant
    great = IIf(a > b, 1&, 0&)
End Function
Function p_great() As Long
    p_great = VBA.CLng(AddressOf great)
End Function
